apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    defaultConfig {
        applicationId "com.example.sam_chordas.stockhawk"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true

        // gotten from https://medium.com/google-developers/building-a-documentsprovider-f7f2fb38e86a
        def actionDataUpdatedValue = applicationId + ".ACTION_DATA_UPDATED"

        // Now we can use ${actionDataUpdated} in our Manifest
        manifestPlaceholders = [ actionDataUpdated: actionDataUpdatedValue ]

        // Now we can use BuildConfig.ACTION_DATA_UPDATED in our code
        buildConfigField "String", "ACTION_DATA_UPDATED", "\"${actionDataUpdatedValue}\""

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    maven { url "https://jitpack.io" }
}

// extensions
ext {
    butterKnifeVersion = "8.5.1"
    constraintLayoutVersion = "1.0.2"
    eventBusVersion = "3.0.0"
    materialDialogsVersion = "0.9.4.4"
    playServicesVersion = "10.0.1"
    schematicVersion = "0.7.0"
    stethoVersion = "1.4.2"
    supportLibraryVersion = "25.0.1"
    williamChartVersion = "2.4.0"
}

dependencies {

    // butterknife
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnifeVersion"
    compile "com.jakewharton:butterknife:$butterKnifeVersion"

    // constraint layout
    //noinspection GradleCompatible
    compile "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"

    // design
    //noinspection GradleCompatible
    compile "com.android.support:design:$supportLibraryVersion"

    // eventbus
    compile "org.greenrobot:eventbus:$eventBusVersion"

    // gcm
    compile "com.google.android.gms:play-services-gcm:$playServicesVersion"

    // material-dialogs
    compile "com.afollestad.material-dialogs:core:$materialDialogsVersion"

    // ok http
    compile 'com.squareup.okhttp:okhttp:2.5.0'

    // schematic
    compile "net.simonvt.schematic:schematic:$schematicVersion"
    annotationProcessor "net.simonvt.schematic:schematic-compiler:$schematicVersion"

    // stetho
    debugCompile "com.facebook.stetho:stetho:$stethoVersion"

    // support library
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"

    // william chart
    compile "com.diogobernardino:williamchart:$williamChartVersion"

}
